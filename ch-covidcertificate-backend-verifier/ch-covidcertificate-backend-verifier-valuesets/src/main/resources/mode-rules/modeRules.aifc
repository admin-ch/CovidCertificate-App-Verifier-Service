import "../rule-constants/test.aifc";

let TWOG_PLUS_PERIOD_VACCINE = 121;
let TWOG_PLUS_PERIOD_RECOVERY = 131;

switch(payload.h.mode){
    "THREE_G": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_EXEMPTION ]) => {
        "SUCCESS"
    }
    "THREE_G" => {
        "SUCCESS"
    }
    "TWO_G": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_EXEMPTION ]) => {
            "SUCCESS"
    }
    "TWO_G": if (payload.v.0) => {
        "SUCCESS"
    }
    "TWO_G": if (payload.r.0) => {
        "SUCCESS"
    }
    "TWO_G" => {
        if (payload.h.isLight){
            "IS_LIGHT"
        }else{
            /* PCR and RAT tests are not valid in 2G, but antibody "tests" are since they count as recovery */
            if(payload.t.0){
                switch(payload.t.0.tt){
                    [ TEST_TYPE_PCR ] => {"INVALID"}
                    /* RAT */
                    [ TEST_TYPE_RAT ] => {
                        if (payload.t.0.tr === TEST_RESULT_POSITIVE ){
                             "SUCCESS"
                        }else{
                             "INVALID"
                        }
                    }
                    _ => {"SUCCESS"}
                }
            } else {
                "INVALID"
           }
        }
    }
    "TWO_G_PLUS": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_EXEMPTION ]) => {
            "SUCCESS"
    }
    "TWO_G_PLUS": if (payload.h.isLight) => {
        /*IS LIGHT*/
        "IS_LIGHT"
    }
    "TWO_G_PLUS": if (payload.v.0) => {
        if((now() + 0#days) is before (payload.v.0.dt as DateTime + TWOG_PLUS_PERIOD_VACCINE #days)){
            /* within 120 days, a test is not required */
            "SUCCESS"
        }else{
            /*First part of 2g plus*/
            "SUCCESS_2G"
        }
    }
    "TWO_G_PLUS": if (payload.r.0) => {
        if((now() + 0#days) is before (payload.r.0.fr as DateTime + TWOG_PLUS_PERIOD_RECOVERY #days)){
           /* within 120 days after the recovery cert becomes valid (after the 10 day waiting period), a test is not required */
           "SUCCESS"
        }else{
           /*First part of 2g plus*/
           "SUCCESS_2G"
        }
    }
    /* positive RAT is equivalent to recovery */
    "TWO_G_PLUS": if (payload.t.0 && payload.t.0.tt in [ TEST_TYPE_RAT ] && payload.t.0.tr === TEST_RESULT_POSITIVE ) => {
        if((now() + 0#days) is before (payload.t.0.sc as DateTime + TWOG_PLUS_PERIOD_RECOVERY #days)){
           /* within 120 days after the recovery cert becomes valid (after the 10 day waiting period), a test is not required */
           "SUCCESS"
        }else{
           /*First part of 2g plus*/
           "SUCCESS_2G"
        }
    }
    "TWO_G_PLUS": if (payload.t.0) => {
        switch(payload.t.0.tt){
            /* PCR tests are the + of 2G+ => SUCCESS_2G_PLUS */
            [ TEST_TYPE_RAT, TEST_TYPE_PCR ] => {"SUCCESS_2G_PLUS"}
            [ TEST_TYPE_SERO ] => {"SUCCESS_2G"}
            _ => {"INVALID"}
        }
    }
    "TWO_G_PLUS" => {
       "INVALID"
    }
    "TEST_CERT": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_EXEMPTION ]) => {
            "SUCCESS"
    }
    "TEST_CERT": if (payload.h.isLight) => {
            /*IS LIGHT*/
            "IS_LIGHT"
    }
    "TEST_CERT": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_PCR ]) => {
            "SUCCESS"
    }
    /* only negative RAT are treated like test certs */
    "TEST_CERT": if(payload.t.0 && payload.t.0.tt in [ TEST_TYPE_RAT ] && payload.t.0.tr === TEST_RESULT_NEGATIVE ) => {
         "SUCCESS"
    }
    "TEST_CERT" => {
        "INVALID"
    }
    _ => {
        "UNKNOWN_MODE"
    }
}
